/**
 * ## test
 * 
 * ### Purpose
 * 
 * Used to generate code for using TDD methodology.  Tries to report useful
 * error messages with an optional user configurable message.
 * 
 * ### Test Your Code!
 */
use <helpers>

/** Tests if `expected` is equal to `got`.
 *
 * @param {any} expected
 *   Expected value.
 * @param {any} got
 *   The value actually received.
 * @param {string} [msg=""]
 *   A user message to append to failure message.
 */
module test_eq(expected, got, msg="") {
  assert(expected == got, str("\n  EXPECTED: ", expected, "\n       GOT: ", got, "\n", msg));
}

/** Tests if `expected` is approx equal to `got` within `epsilon`.
 *
 * @param {any} expected
 *   Expected value.
 * @param {any} got
 *   The value actually received.
 * @param {number} epsilon
 *   How much tolerance to say that two numbers are equal.
 * @param {string} [msg=""]
 *   A user message to append to failure message.
 */

module test_approx_eq(expected, got, epsilon, msg="") {
  assert(equal(expected, got, epsilon), str("\n  EXPECTED: ", expected, "\n       GOT: ", got, "\n", msg));
}

/** Tests if `not_expected` is not equal to `got`. */
module test_ne(not_expected, got, msg="") {
  assert(not_expected != got, str("\n  SHOULD NOT HAVE GOTTEN: \"", got, "\"\n", msg));
}

/** Tests if `lhs < rhs`. */
module test_lt(lhs, rhs, msg="") {
  assert(lhs < rhs, str("\n  ", lhs, " < ", rhs, " IS FALSE!\n", msg));
}

/** Tests if `lhs ≤ rhs`. */
module test_le(lhs, rhs, msg="") {
  assert(lhs <= rhs, str("\n  ", lhs, " ≤ ", rhs, " IS FALSE!\n", msg));
}

/** Tests if `lhs > rhs`. */
module test_gt(lhs, rhs, msg="") {
  assert(lhs > rhs, str("\n  ", lhs, " > ", rhs, " IS FALSE!\n", msg));
}

/** Tests if `lhs ≥ rhs`. */
module test_ge(lhs, rhs, msg="") {
  assert(lhs >= rhs, str("\n  ", lhs, " ≥ ", rhs, " IS FALSE!\n", msg));
}

/** Tests if `val` is a truthy value */
module test_truthy(val, msg="") {
  assert(val, str("\n  ", val, " IS NOT TRUTHY!\n", msg));
}

/** Tests if `val` is a falsy value */
module test_falsy(val, msg="") {
  assert(!val, str("\n  !", val, " IS NOT FALSY!\n", msg));
}

// test_eq(1, 2, "should be equal");
// test_ne(1, 1);
// test_lt(1, 1);
// test_gt(1, 1);
// test_le(1, 0);
// test_ge(1, 2);
// test_truthy(0);
// test_falsy(1);
test_falsy([]);
test_falsy("");
test_falsy(0);
test_truthy([0]);
test_truthy("0");
test_truthy(!0);