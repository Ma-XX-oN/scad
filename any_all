/**
 * ## any_all
 *
 * ### How to Import
 *
 *     use <any_all>
 *
 * ### Purpose
 *
 * Implements any and all algorithms.
 */
use <base_algos>

/**
 * @callback AnyFn
 * @param {PredFn} pred_fn
 * @returns {bool}
 *   First index where `pred_fn(i)` is `true` will result in `true`, else
 *   `false`.
 */

/**
 * @typedef {AlgoFn} AnyBirleiFn
 *
 * @returns {AnyFn}
 *   Callback that returns first index where `pred_fn(i)` is `true` will result
 *   in `true`, else `false`.
 */

/**
 * @callchain any() (birl, end_i) : bool
 * @callchain any(birl, end_i)    : bool
 *
 * Will determine if any of the calls to pred_fn(i) will result in a truthy
 * result.
 *
 * @param {OptionalBirl} [birl]
 * @param {EndI} [end_i]
 *
 * @returns {(AnyFn|bool)}
 *   - If `birl` is omitted, then will return type `AnyBirleiFn`.
 *   - Else returns type `AnyFn`.
 */
function any(birl, end_i) =
  is_undef(birl)
  ? function(birl, end_i) any(birl, end_i)
  : function(any_pred_fn)
      !is_undef(find(birl, end_i)(any_pred_fn))
;

/**
 * @callback AllFn
 * @param {PredFn} pred_fn
 * @returns {(number|undef)}
 *   First index where `pred_fn(i)` is `false` will result in `false`, else
 *   `true`.
 */

/**
 * @typedef {AlgoFn} AllBirleiFn
 *
 * @returns {AllFn}
 *   Callback that returns first index where `pred_fn(i)` is `false` will result
 *   in `false`, else `true`.
 */

/**
 * @callchain all()            : AllBirleiFn
 * @callchain all(birl, end_i) : bool
 *
 * Will determine if all of the calls to pred_fn(i) will result in a truthy
 * result.
 *
 * @param {OptionalBirl} [birl]
 * @param {EndI} [end_i]
 *
 * @returns {function(pred_fn: PredFn): bool}
 *   If all pred_fn(i) calls returns truthy, then returns true, otherwise
 *   false.
 */
function all(birl, end_i) =
  is_undef(birl)
  ? function(birl, end_i) all(birl, end_i)
  : function(all_pred_fn)
      is_undef(find(birl, end_i)(function(v) !all_pred_fn(v)))
;
